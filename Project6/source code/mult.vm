// Function calculates x * y
// x and y are stored in (argument, 0) and (argument, 1), respectively.
// Result is stored in (local, 0)
// Base address of the argument segment: 400
// Base address of the local segment: 300
// Author: Naga Kandasamy
// Date created: November 12, 2020
// Pseudo code:
// int sum;
// sum = 0;
// for (i = y; i != 0; i--) 
//      sum = sum + x;

function mult 2 
push constant 0
pop local 0         // sum = 0
push argument 1 
pop local 1         // i = y
label LOOP 
push constant 0 
push local 1 
eq 
if-goto END         // if i == 0, goto END
push local 0
push argument 0
add
pop local 0         // sum = sum + x
push local 1
push constant 1
sub                 
pop local 1         // i = i - 1
goto LOOP
label END
push local 0
return